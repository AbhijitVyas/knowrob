%%
:- { label: 'Person' },
   (Man or Woman)(?x) -> Person(?x).
%%
:- { label: 'Hermaphrodite' },
   (Man and Woman)(?x) -> Hermaphrodite(?x). 
%%
:- { label: 'Driver' },
   Person(?p), hasCar(?p,true)
-> Driver(?p).
%%
:- { label: 'DriverFred' },
   Person(Fred), hasCar(Fred,true) -> Driver(Fred).
%%
:- { label: 'greaterThen' },
   Person(?p), hasAge(?p,?age), greaterThan(?age,17)
-> Adult(?p). 
%%
:- { label: 'Adult2' },
   (Driver or (Person and (hasChild value true)))(?x)
-> Adult(?x).
%%
:- { label: 'brother' },
   Person(?p), hasSibling(?p,?s), Man(?s)
-> hasBrother(?p,?s).
%%
:- { label: 'startsWith' },
   Person(?p), hasNumber(?p,?number), startsWith(?number,"+")
-> hasInternationalNumber(?p,true).
%%
:- { label: 'exactly' },
   Person(?p), (hasSibling exactly 0 Thing)(?p)
-> Singleton(?p).
%%
:- { label: 'area' },
   Rectangle(?r), hasWidthInMeters(?r,?w), 
                  hasHeightInMeters(?r,?h),
                  multiply(?area,?w,?h),
                  greaterThan(?area,100)
-> hasAreaInSquareMeters(?r,?area).
%%
:- { label: 'BigRectangle' },
   Rectangle(?r), hasWidthInMeters(?r,?w), 
                  hasHeightInMeters(?r,?h),
                  multiply(?area,?w,?h),
                  greaterThan(?area,100)
-> BigRectangle(?r).
%%
:- { label: 'NonHuman' },
   TestThing(?x), (not Person)(?x) -> NonHuman(?x). 
